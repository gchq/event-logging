ext.moduleName = 'event.logging.generator'

def schemaDir = project.file('schema')

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation libs.jaxb_xjc
    implementation libs.slf4j_api

    runtimeOnly libs.jaxb_runtime_2
    runtimeOnly libs.jaxb_rich_contract_plugin
    runtimeOnly libs.jaxb_basics
    runtimeOnly libs.saxon_he
}

task clearSchemaDir(type: Delete) {
    //ensure the import directory exists
    schemaDir.mkdirs()

    //cleans out any zip files in the contentPackImportDir
    delete fileTree(schemaDir) {
        include '**/*.xsd'
    }
}

task downloadSchema() {

    dependsOn clearSchemaDir

    doLast {
        schemaDir.mkdirs()
        if (!eventLoggingSchemaFilePath.isEmpty()) {
            println "\nWARNING: Using local schema instead of released version from GitHub\n"
            println "Reading schema from local file ${eventLoggingSchemaFilePath}"

            def schemaFile = new File(eventLoggingSchemaFilePath)

            copy {
                from schemaFile
                into schemaDir
            } 
        } else {
            println "Downloading schema from ${eventLoggingSchemaUrl}"
            download {
                src eventLoggingSchemaUrl
                dest schemaDir
                overwrite true
            }
        }
    }
}


task runGenClasses(type: JavaExec) {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))

    dependsOn downloadSchema

    classpath = sourceSets.main.runtimeClasspath

    mainClass = 'event.logging.gen.GenClasses'
}


clean{
    //the contents of src is transient so clear it out
    delete "schema"
}
